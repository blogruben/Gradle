plugins {
    id 'java'
    id 'application'
}

def clasePrincipal = 'simple.App'
def directorioApp = 'App'
def nombreDelJar = 'App' 
def directorioLibs = 'librerias'

application {
    //clase principal para "gradlew -q run"
    mainClassName = "$clasePrincipal"
}

repositories {
    mavenCentral()
}

dependencies {
   compile 'org.apache.commons:commons-math3:3.6.1'
   //no se necesita en compilacion porque no se importa en ningun momento.
   runtimeOnly 'org.apache.commons:commons-lang3:3.8'  
   compile 'joda-time:joda-time:2.12.2'
 }

jar {
    //a√±adimos al manifest
    manifest {
        attributes("Implementation-Title": project.name,
                  "Implementation-Version": "01.00.00",
                  "Main-Class": "$clasePrincipal",
                  //runtimeClasspath tambien incluye runtimeOnly, compile o runtime no lo incluye.
                  "Class-Path": configurations.runtimeClasspath.collect { "${directorioLibs}/${it.getName()}" }.join(' ')) 

    }
    //nombre del jar
    baseName "$nombreDelJar"
}


task generarCompilado {
    doLast {
        //Crear directorio App con el jar y el properties
        project.delete "$directorioApp"
        project.mkdir "$directorioApp"
        copy {
            from "build/libs/${nombreDelJar}.jar"
            into "$directorioApp"
        }
        copy {
            from configurations.runtimeClasspath 
            into file("${directorioApp}/${directorioLibs}") 
        }
        println("Generado compilado en: ${directorioApp}/${nombreDelJar}.jar")
        println("Ejecutar con: java -jar ${directorioApp}/${nombreDelJar}.jar")
    }
}

build.finalizedBy(generarCompilado) // despues del build
