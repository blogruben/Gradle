plugins {
    id 'java'
}

//gradle argumentosFijos
task argumentosFijos (type: JavaExec){
    main = 'org.java.ruben.ejecutar.App'
    classpath = sourceSets.main.runtimeClasspath

    args = ["arg1", "arg2", "arg3"]
}

//gradle pasarArgumentos -Dexec.args="arg1 arg2 arg3"
task pasarArgumentos (type: JavaExec){
    main = 'org.java.ruben.ejecutar.App'
    classpath = sourceSets.main.runtimeClasspath

    /* Pasamos los argumentos */
    args System.getProperty("exec.args", "").split()
}

ext.javaMainClass = 'org.java.ruben.ejecutar.App'
if (project.hasProperty("args")) {
    ext.cmdargs = project.getProperty("args")
} else { 
    ext.cmdargs = ""
}

//gradle pasarPropiedadesConArgumentos --args="lorem ipsum dolor" -Dmyvariable=unValor
task leerVariables (type: JavaExec){
    group = "Execution"
    description = "Run the main class with pasarArgumentosPropiedadesAOtroMetodo"
    classpath = sourceSets.main.runtimeClasspath
    main = javaMainClass
    args cmdargs.split()
    systemProperties System.getProperties()
}

//The -P flag is for gradle properties, and the -D flag is for JVM properties
//gradle pasarPropiedadesConArgumentos --args="lorem ipsum dolor" -Dmyvariable=unValor -Psaludo=hola
task pasarPropiedadesConArgumentos (type: JavaExec){
    group = "Execution"
    description = "Run the main class with pasarArgumentosPropiedadesAOtroMetodo"
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.java.ruben.ejecutar.App'
    if (project.hasProperty("args")) {
        args project.getProperty("args").split()
    } 
    systemProperties['applicationName'] = project.getProperty("applicationName")
    if (project.hasProperty("saludo")) {
        systemProperties['saludar'] = project.getProperty("saludo")
    }
    systemProperties System.getProperties()
}

//The -P flag is for gradle properties, and the -D flag is for JVM properties
//gradle verPropiedades -Dmyvariable=unValor -Psaludo=hola
//1.applicationName GradleApp
//2.projectName: Nombre proyecto
//3.myvariable: unValor
//4.saludo: hola
task verPropiedades {
    doFirst {
        println System.getProperty("projectName")
        println "1.applicationName "+applicationName   
        println project.getProperty("projectName") ? "2.projectName: "+project.getProperty("projectName") : 'no message'
        println "3.myvariable: "+System.getProperty("myvariable")   
        if (project.hasProperty("saludo")) {
            println "4.saludo: "+project.getProperty("saludo")
        } 
        println System.getProperty("mymessage") ? System.getProperty("mymessage") : 'no message'

        //for(Map.Entry p: project.getProperties()) {
        //    println p;
        //}
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
task wrapVersion(type: Wrapper) {
   gradleVersion = '5.6' 
}

